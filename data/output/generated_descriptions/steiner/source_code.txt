% The ternary Steiner problem of order n is to find n(n-1)/6 sets of elements in {1,2,...,n}
% such that each set contains three elements and any two sets have at most one element in common.
% For example, the following shows a solution for size n=7:
%
%      {1,2,3}, {1,4,5}, {1,6,7}, {2,4,6}, {2,5,7}, {3,4,7}, {3,5,6}

include "globals.mzn";

int: N = 7; 
int: NB = N *(N-1) div 6;
array[1..NB] of var set of 1..N: Sets;

% solve satisfy;
solve::set_search(Sets, first_fail, indomain_min, complete) satisfy;



constraint
   forall(i in index_set(Sets)) (
      card(Sets[i]) = 3
   )
   /\ 
   forall(i,j in index_set(Sets) where i < j) (
      card( Sets[i] intersect Sets[j]) <= 1
   )
   /\ % symmetry breaking
   decreasing(Sets)

;

output [
       "N: ", show(N), " NB: ", show(NB), "\n",
       "Sets: ", show(Sets)
];

